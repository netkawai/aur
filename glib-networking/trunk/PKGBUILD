# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=glib-networking
pkgver=2.65.90
pkgrel=1
pkgdesc="Network extensions for GLib"
url="https://gitlab.gnome.org/GNOME/glib-networking"
arch=(x86_64)
license=(GPL2)
depends=(glib2 libproxy gnutls) # gsettings-desktop-schemas)
makedepends=(meson git)
checkdepends=(ca-certificates)
# _commit=533d3a76e2cc622b072e3ec789f69e888f3fd8eb  # tags/2.64.3^0
source=("git+https://gitlab.gnome.org/GNOME/glib-networking.git")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  meson $pkgname build --prefix=/usr
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set ts=2 sw=2 et:
